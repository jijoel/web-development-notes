Friday, 17 April 2015
=====================
I'm struggling today with VBA. Specifically, I need to print a receipt from the new web payment form. I need to pass a payment ID to the vba shell, and tell vba to print the receipt. Really, is there any way for Javascript to send a message to a containing object?

My pages (keeping in mind that my subforms are using ajax to load):

http://test.dev/:

```js
<!DOCTYPE html>
<html>
<head>
    <title>Test</title>
</head>
<body>

This is a test
<button type="button" id="print_receipt" onclick="show_message()" data-id="">Receipt button</button>

<div id="result"></div>

<script src="/js/jquery-1.11.2.min.js"></script>
<script type="text/javascript">

    $(function(){
        $('#result').load('/test2');
    });

    function show_message() {
        alert('message from javascript: ' + $('#print_receipt').attr('data-id'));
    }

</script>
</body>
</html>
```

http://test.dev/test2:

```js
<button type="button" id="test1" onclick="callVBA()">Test 1</button>

<script type="text/javascript">
    
    function callVBA()
    {
        $('#print_receipt').attr('data-id', 'test1');
        $('#print_receipt').click();

        $('#result').load('/test3');
    }

</script>
```

http://test.dev/test3:

```js
Test3 is loaded...
<button type="button" id="test3">button</button>

<script type="text/javascript">    

    $('#test3').click(function(){
        $('#print_receipt').attr('data-id','test3 button');
        $('#print_receipt').click();
        alert('test3 clicked');
    });

</script>
```

In a browser, it works well. The main page automatically loads test2. I can click the button on test2 to set the value in #print_receipt (and click it) and load test3. I can click the button in test3 to set the value in #print_receipt and click it. Everything is good.

The problem happens when I try to run it from VB.

My form:

```
Option Compare Database

Private Sub Form_Load()
    Me.WebBrowser0.Navigate2 "http://test.dev"
End Sub

Private Sub WebBrowser0_DocumentComplete(ByVal pDisp As Object, URL As Variant)
    
    Set cfForward = New JavascriptConnector
    cfForward.Set_Destination Me, "Print_Receipt"
    WebBrowser0.Document.All("print_receipt").OnClick = cfForward
    
End Sub

Public Function Print_Receipt()

    receipt_value = WebBrowser0.Document.All("print_receipt").getAttribute("data-id")
    MsgBox "Printing Receipt: " & receipt_value
    
End Function
```

The JavascriptConnector object:

```
' JavascriptConnector
' Enable Javascript events in a WebBrowser control to
' bubble up to Access
'
Option Compare Database
Option Explicit

Dim oObject As Object
Dim sMethod As String
Dim bInstantiated As Boolean

Public Sub Class_Initialize()

    bInstantiated = False
  
End Sub

Public Sub Set_Destination(oInObject As Object, sInMethod As String)

    Set oObject = oInObject
    sMethod = sInMethod
    bInstantiated = True
  
End Sub

Public Sub Call_VB_From_Javascript()
    Attribute Value.VB_UserMemId = 0
    
    If bInstantiated Then
        CallByName oObject, sMethod, VbMethod
    End If
  
End Sub
```

NOTE: To get the Attribute Value set in Call_VB_From_Javascript, I needed to export the module, enter that line by hand, then import the module. There's apparently no interface to it...

Whenever I click the #print_receipt button, it successfully runs Print_Receipt from the form. The problem is that clicking any of the other buttons does not appear to click the #print_receipt button (or, at least, that doesn't bubble to the form).

It's not erroring out. #test3 will change the value, and display its own message, but the #print_receipt message does not get printed.

Aha! I just found the answer! In the Javascript, I can not use jQuery to run the click() event. I need to use the DOM objects. So, instead of this:

    $('#print_receipt').attr('data-id', 'test1');
    $('#print_receipt').click();

I use:

    $('#print_receipt').attr('data-id', 'test1');
    document.getElementById('print_receipt').click();

And then, everything works perfectly. Yay! I should now be able to send pretty much anything to my VBA scripts, from Javascript, in realtime. As things change in Javascript, we can reset things in VBA. Until such a time that we don't need to use VBA any more.



